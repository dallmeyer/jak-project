;;-*-Lisp-*-
(in-package goal)

(define *speedrun-info* (new 'static 'speedrun-info-jak1))

(defun start-speedrun ((category speedrun-category))
  ;; randomize game id so the autosplitter knows to restart
  (update-autosplit-jak1-new-game)
  ;; disable hints (this seems to be overriden by your slot 1 save though)
  (set! (-> *setting-control* default play-hints) #f)
  ;; ensure `force actors` is not enabled
  (set! (-> *pc-settings* force-actors?) #f)
  ;; force FPS to `60`
  (set-frame-rate! *pc-settings* 60)
  ;; enable auto saving by default
  (set! (-> *setting-control* default auto-save) #t)
  ;; set flag for post-blackout run setup
  (set! (-> *speedrun-info* needs-setup?) #t)

  (set! (-> *speedrun-info* category) category)
  (case category
    (((speedrun-category full-game-any))
      ;; spawn at the warp gate checkpoint
      (initialize! *game-info* 'game (the-as game-save #f) "game-start")
      )
    (((speedrun-category il-training))
      ;; spawn at the warp gate checkpoint
      (initialize! *game-info* 'game (the-as game-save #f) "game-start")
      )
    (((speedrun-category il-village1))
      ;; spawn at the green sage hut warp gate
      (initialize! *game-info* 'game (the-as game-save #f) "village1-warp")
      )
    (((speedrun-category il-beach))
      ;; spawn at beach
      (initialize! *game-info* 'game (the-as game-save #f) "beach-start")
      )
    (((speedrun-category il-jungle))
      ;; spawn at start of FJ
      (initialize! *game-info* 'game (the-as game-save #f) "jungle-start")
      )
    (((speedrun-category il-misty))
      ;; spawn at start of misty     
      (initialize! *game-info* 'game (the-as game-save #f) "misty-start")
      )
    (((speedrun-category il-firecanyon))
      ;; spawn at start of fire canyon
      (initialize! *game-info* 'game (the-as game-save #f) "firecanyon-start")
      )
    (((speedrun-category il-village2))
      ;; spawn at rock village start
      (initialize! *game-info* 'game (the-as game-save #f) "village2-start")
      )
    (((speedrun-category il-sunken))
      ;; spawn at start of LPC
      (initialize! *game-info* 'game (the-as game-save #f) "sunken-start")
      )
    (((speedrun-category il-swamp))
      ;; spawn at start of boggy
      (initialize! *game-info* 'game (the-as game-save #f) "swamp-start")
      )
    (((speedrun-category il-rolling))
      ;; spawn at start of basin
      (initialize! *game-info* 'game (the-as game-save #f) "rolling-start")
      )
    (((speedrun-category il-ogre))
      ;; spawn before klaww
      (initialize! *game-info* 'game (the-as game-save #f) "ogre-start")
      )
    (((speedrun-category il-village3))
      ;; spawn at start of volcanic crater
      (initialize! *game-info* 'game (the-as game-save #f) "village3-start")
      )
    (((speedrun-category il-snow))
      ;; spawn at start of snowy
      (initialize! *game-info* 'game (the-as game-save #f) "snow-start")
      )
    (((speedrun-category il-cave))
      ;; spawn at start of spider cave
      (initialize! *game-info* 'game (the-as game-save #f) "maincave-start")
      )
    (((speedrun-category il-lavatube))
      ;; spawn at start of lava tube
      (initialize! *game-info* 'game (the-as game-save #f) "lavatube-start")
      )
    (((speedrun-category il-citadel))
      ;; spawn outside first citadel door
      (initialize! *game-info* 'game (the-as game-save #f) "citadel-entrance")
      )
    )
  (none)
  )

(defun setup-speedun ()
  ;; check if target is spawned and blackout frames are over
  (when (and *target* (> (- (-> *display* base-frame-counter) (-> *game-info* blackout-time)) 0))
    (set! (-> *speedrun-info* needs-setup?) #f)
    (case (-> *speedrun-info* category)
      (((speedrun-category full-game-any))
        ;; skip intro cutscene
        (close-specific-task! (game-task intro) (task-status need-resolution))
        )
      (((speedrun-category il-training))
        ;; skip intro cutscene
        (close-specific-task! (game-task intro) (task-status need-resolution))
        )
      (((speedrun-category il-village1))
        ;; give enough orbs to buy all cells 120+120+90+90=420
        (set! (-> *game-info* money-total) 420.0)
        (set! (-> *game-info* money) 420.0)
        )
      (((speedrun-category il-beach))
        ;; unlock blue eco vent
        (close-specific-task! (game-task jungle-eggtop) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task jungle-eggtop)))
        )
      (((speedrun-category il-jungle))
        )
      (((speedrun-category il-misty))
        ;; unlock fisherman's boat
        (close-specific-task! (game-task jungle-fishgame) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task jungle-fishgame)))
        )
      (((speedrun-category il-firecanyon))
        ;; skip keira intro cutscene
        (close-specific-task! (game-task firecanyon-assistant) (task-status need-reward-speech))
        ;; unlock blue eco vent
        (close-specific-task! (game-task jungle-eggtop) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task jungle-eggtop)))
        )
      (((speedrun-category il-village2))
        ;; give enough orbs to buy all cells 120+120+90+90+90=510
        (set! (-> *game-info* money-total) 510.0)
        (set! (-> *game-info* money) 510.0)
        )
      (((speedrun-category il-sunken))
        )
      (((speedrun-category il-swamp))
        ;; unlock flutflut
        (close-specific-task! (game-task beach-flutflut) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task beach-flutflut)))
        (close-specific-task! (game-task village2-levitator) (task-status need-introduction))
        )
      (((speedrun-category il-rolling))
        ;; unlock zoomer
        (close-specific-task! (game-task village2-levitator) (task-status need-introduction))
        ;; unlock DMG
        (close-specific-task! (game-task rolling-race) (task-status need-introduction))
        ;; unlock moles
        (close-specific-task! (game-task rolling-moles) (task-status need-introduction))
        )
      (((speedrun-category il-ogre))
        ;; skip klaww intro cutscene
        (close-specific-task! (game-task ogre-boss) (task-status need-hint))
        ;; unlock yellow eco vent
        (close-specific-task! (game-task snow-eggtop) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task snow-eggtop)))
        )
      (((speedrun-category il-village3))
        ;; give enough orbs to buy all cells 120+120+90+90+90+90=600
        (set! (-> *game-info* money-total) 600.0)
        (set! (-> *game-info* money) 600.0)
        )
      (((speedrun-category il-snow))
        ;; unlock flutflut
        (close-specific-task! (game-task beach-flutflut) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task beach-flutflut)))
        ;; unlock gondola
        (close-specific-task! (game-task village3-button) (task-status need-introduction))
        )
      (((speedrun-category il-cave))
        ;; unlock yellow eco vent
        (close-specific-task! (game-task snow-eggtop) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task snow-eggtop)))
        )
      (((speedrun-category il-lavatube))
        ;; skip keira intro cutscene
        (close-specific-task! (game-task lavatube-start) (task-status need-reward-speech))
        )
      (((speedrun-category il-citadel))
        ;; remove invisible wall
        (close-specific-task! (game-task lavatube-end) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task lavatube-end)))
        ;; unlock door
        (close-specific-task! (game-task village4-button) (task-status need-reward-speech))
        )
      )
    )
  (none)
  )

(defun speedrun-mode-update ()
  "A per frame update for speedrunning related stuff"
  (when (-> *pc-settings* speedrunner-mode?)
    ;; Update auto-splitter
    (update-autosplit-info-jak1)
    ;; Draw info to the screen
    (speedrun-draw-settings)
    ;; Run speedrun setup if needed
    (when (-> *speedrun-info* needs-setup?)
      (setup-speedun))
    )
  (none))

(defun speedrun-start-run ()
  ;; start a full game speedrun using any% as a placeholder for now
  (start-speedrun (speedrun-category full-game-any))
  (none))

(defun speedrun-draw-settings ()
  "Draw speedrun related settings in the bottom left corner"
  (when (and (-> *pc-settings* speedrunner-mode?)
             (< (-> *autosplit-info-jak1* num-power-cells) 1))
    (with-dma-buffer-add-bucket ((buf (-> (current-frame) global-buf))
                                      (bucket-id debug-no-zbuf))
      (draw-string-xy (string-format "OpenGOAL Version: ~S ~%Speedrun mode: ~A ~%Cutscene Skips ~A"
                                     *pc-settings-built-sha*
                                     (-> *pc-settings* speedrunner-mode?)
                                     (-> *pc-settings* cutscene-skips?))
                      buf 0 (- 224 (* 8 4)) (font-color flat-yellow) (font-flags shadow kerning))))
  (none))
