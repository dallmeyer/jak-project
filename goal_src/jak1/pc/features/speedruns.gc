;;-*-Lisp-*-
(in-package goal)

(define *speedrun-info* (new 'static 'speedrun-info-jak1 :should-display? #t :should-display-deload-info? #f :needs-post-blackout-setup? #f))

(define *last-speedrun-practice-option* (speedrun-practice-option none))
(define *practice-spawn-post-init* (the-as (function none) #f))

(define *last-real-continue-point* (the-as continue-point #f))

(define *hub1-cell-list* (new 'static 'boxed-array :type game-task
  (game-task training-gimmie)
  (game-task training-door)
  (game-task training-climb)
  (game-task training-buzzer)
  (game-task village1-mayor-money)
  (game-task village1-uncle-money)
  (game-task village1-yakow)
  (game-task village1-oracle-money1)
  (game-task village1-oracle-money2)
  (game-task village1-buzzer)
  (game-task beach-ecorocks)
  (game-task beach-flutflut)
  (game-task beach-pelican)
  (game-task beach-seagull)
  (game-task beach-cannon)
  (game-task beach-gimmie)
  (game-task beach-sentinel)
  (game-task beach-buzzer)
  (game-task jungle-lurkerm)
  (game-task jungle-tower)
  (game-task jungle-eggtop)
  (game-task jungle-plant)
  (game-task jungle-fishgame)
  (game-task jungle-canyon-end)
  (game-task jungle-temple-door)
  (game-task jungle-buzzer)
  (game-task misty-muse)
  (game-task misty-boat)
  (game-task misty-cannon)
  (game-task misty-warehouse)
  (game-task misty-bike)
  (game-task misty-bike-jump)
  (game-task misty-eco-challenge)
  (game-task misty-buzzer)
  (game-task firecanyon-end)
  (game-task firecanyon-buzzer)
  ))

(define *hub2-cell-list* (new 'static 'boxed-array :type game-task
  (game-task village2-gambler-money)
  (game-task village2-geologist-money)
  (game-task village2-warrior-money)
  (game-task village2-oracle-money1)
  (game-task village2-oracle-money2)
  (game-task village2-buzzer)
  (game-task sunken-room)
  (game-task sunken-pipe)
  (game-task sunken-slide)
  (game-task sunken-sharks)
  (game-task sunken-platforms)
  (game-task sunken-top-of-helix)
  (game-task sunken-spinning-room)
  (game-task sunken-buzzer)
  (game-task swamp-flutflut)
  (game-task swamp-billy)
  (game-task swamp-battle)
  (game-task swamp-tether-4)
  (game-task swamp-tether-1)
  (game-task swamp-tether-2)
  (game-task swamp-tether-3)
  (game-task swamp-buzzer)
  (game-task rolling-moles)
  (game-task rolling-robbers)
  (game-task rolling-race)
  (game-task rolling-lake)
  (game-task rolling-plants)
  (game-task rolling-ring-chase-1)
  (game-task rolling-ring-chase-2)
  (game-task rolling-buzzer)
  (game-task ogre-boss)
  (game-task ogre-end)
  (game-task ogre-secret)
  (game-task ogre-buzzer)
  ))

(define *hub3-cell-list* (new 'static 'boxed-array :type game-task
  (game-task village3-miner-money1)
  (game-task village3-miner-money2)
  (game-task village3-miner-money3)
  (game-task village3-miner-money4)
  (game-task village3-oracle-money1)
  (game-task village3-oracle-money2)
  (game-task village3-extra1)
  (game-task village3-buzzer)
  (game-task snow-eggtop)
  (game-task snow-ram)
  (game-task snow-bumpers)
  (game-task snow-cage)
  (game-task snow-fort)
  (game-task snow-ball)
  (game-task snow-bunnies)
  (game-task snow-buzzer)
  (game-task cave-gnawers)
  (game-task cave-dark-crystals)
  (game-task cave-dark-climb)
  (game-task cave-robot-climb)
  (game-task cave-swing-poles)
  (game-task cave-spider-tunnel)
  (game-task cave-platforms)
  (game-task cave-buzzer)
  (game-task lavatube-end)
  (game-task lavatube-buzzer)
  ))

;; unused
;; (define *citadel-cell-list* (new 'static 'boxed-array :type game-task
;;   (game-task citadel-sage-blue)
;;   (game-task citadel-sage-red)
;;   (game-task citadel-sage-yellow)
;;   (game-task citadel-sage-green)
;;   (game-task citadel-buzzer)
;;   ))

(defun is-speedrun-category? ((category symbol))
  (and (-> *pc-settings* speedrunner-mode?)
        (name= category (enum->string speedrun-category (-> *speedrun-info* category)))
        )
  )

(defun start-speedrun ((category speedrun-category))
  ;; randomize game id so the autosplitter knows to restart
  (update-autosplit-jak1-new-game)
  ;; disable hints (turned back on later for all-cutscenes category)
  (set! (-> *setting-control* default play-hints) #f)
  ;; turn on speedrun verification display and flip flag for setting up speedrun after initialize!
  (set! (-> *speedrun-info* should-display?) #t)
  (set! (-> *speedrun-info* needs-post-blackout-setup?) #t)
  ;; start new game with specified checkpoint, if any (otherwise we're resetting current category)
  (if category
    (set! (-> *speedrun-info* category) category)
    )
  (case (-> *speedrun-info* category)
    (((speedrun-category full-game))
      ;; spawn at the geyser warp gate checkpoint
      (initialize! *game-info* 'game (the-as game-save #f) "game-start")
      )
    (((speedrun-category il-training))
      ;; spawn at the geyser warp gate checkpoint
      (initialize! *game-info* 'game (the-as game-save #f) "game-start")
      )
    (((speedrun-category il-village1))
      ;; spawn at the green sage hut warp gate
      (initialize! *game-info* 'game (the-as game-save #f) "village1-warp")
      )
    (((speedrun-category il-beach))
      ;; spawn at beach
      (initialize! *game-info* 'game (the-as game-save #f) "beach-start")
      )
    (((speedrun-category il-jungle))
      ;; spawn at start of FJ
      (initialize! *game-info* 'game (the-as game-save #f) "jungle-start")
      )
    (((speedrun-category il-misty))
      ;; spawn at start of misty     
      (initialize! *game-info* 'game (the-as game-save #f) "misty-start")
      )
    (((speedrun-category il-firecanyon))
      ;; spawn at start of fire canyon
      (initialize! *game-info* 'game (the-as game-save #f) "firecanyon-start")
      )
    (((speedrun-category il-village2))
      ;; spawn at rock village start
      (initialize! *game-info* 'game (the-as game-save #f) "village2-start")
      )
    (((speedrun-category il-sunken))
      ;; spawn at start of LPC
      (initialize! *game-info* 'game (the-as game-save #f) "sunken-start")
      )
    (((speedrun-category il-swamp))
      ;; spawn at start of boggy
      (initialize! *game-info* 'game (the-as game-save #f) "swamp-start")
      )
    (((speedrun-category il-rolling))
      ;; spawn at start of basin
      (initialize! *game-info* 'game (the-as game-save #f) "rolling-start")
      )
    (((speedrun-category il-ogre))
      ;; spawn before klaww
      (initialize! *game-info* 'game (the-as game-save #f) "ogre-start")
      )
    (((speedrun-category il-village3))
      ;; spawn at start of volcanic crater
      (initialize! *game-info* 'game (the-as game-save #f) "village3-start")
      )
    (((speedrun-category il-snow))
      ;; spawn at start of snowy
      (initialize! *game-info* 'game (the-as game-save #f) "snow-start")
      )
    (((speedrun-category il-cave))
      ;; spawn at start of spider cave
      (initialize! *game-info* 'game (the-as game-save #f) "maincave-start")
      )
    (((speedrun-category il-lavatube))
      ;; spawn at start of lava tube
      (initialize! *game-info* 'game (the-as game-save #f) "lavatube-start")
      )
    (((speedrun-category il-citadel))
      ;; spawn outside first citadel door
      (initialize! *game-info* 'game (the-as game-save #f) "citadel-entrance")
      )
    (((speedrun-category new-game-plus))
      ;; spawn at the geyser warp gate checkpoint
      (initialize! *game-info* 'game (the-as game-save #f) "game-start")
      )
    (((speedrun-category hub1-100))
      ;; spawn at the geyser warp gate checkpoint
      (initialize! *game-info* 'game (the-as game-save #f) "game-start")
      )
    (((speedrun-category hub2-100))
      ;; spawn at rock village start
      (initialize! *game-info* 'game (the-as game-save #f) "village2-start")
      )
    (((speedrun-category hub3-100))
      ;; spawn at start of volcanic crater
      (initialize! *game-info* 'game (the-as game-save #f) "village3-start")
      )
    (((speedrun-category all-cutscenes))
      ;; turn play hints back on
      (set! (-> *setting-control* default play-hints) #t)
      ;; don't allow cutscene skips
      (set! (-> *pc-settings* skip-movies?) #f)
      ;; spawn at the geyser warp gate checkpoint
      (initialize! *game-info* 'game (the-as game-save #f) "intro-start")
      )
    (else
      (format 0 "start-speedrun: unrecognized category ~S~%" (enum->string speedrun-category (-> *speedrun-info* category)))
      )
    )
  ;; ensure actor vis is enabled
  (set! (-> *pc-settings* ps2-actor-vis?) #t)
  ;; force FPS to `60`
  (set-frame-rate! *pc-settings* 60 #t)
  ;; enable auto saving by default
  (set! (-> *setting-control* default auto-save) #t)
  (none)
  )
  
(defun setup-speedrun-post-blackout ()
  (when (and (-> *speedrun-info* needs-post-blackout-setup?) (>= (-> *display* base-frame-counter) (-> *game-info* blackout-time)))
    (set! (-> *speedrun-info* needs-post-blackout-setup?) #f)
    (case (-> *speedrun-info* category)
      (((speedrun-category full-game))
        ;; skip intro cutscene
        (close-specific-task! (game-task intro) (task-status need-resolution))
        )
      (((speedrun-category il-training))
        ;; skip intro cutscene
        (close-specific-task! (game-task intro) (task-status need-resolution))
        (mark-text-as-seen *game-info* (text-id training-level-name))
        )
      (((speedrun-category il-village1))
        ;; give enough orbs to buy all cells 120+120+90+90=420
        (set! (-> *game-info* money-total) 420.0)
        (set! (-> *game-info* money) 420.0)
        (mark-text-as-seen *game-info* (text-id village1-level-name))
        )
      (((speedrun-category il-beach))
        ;; unlock blue eco vent
        (close-specific-task! (game-task jungle-eggtop) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task jungle-eggtop)))
        (mark-text-as-seen *game-info* (text-id beach-level-name))
        )
      (((speedrun-category il-jungle))
        (mark-text-as-seen *game-info* (text-id jungle-level-name))
        )
      (((speedrun-category il-misty))
        ;; unlock fisherman's boat
        (close-specific-task! (game-task jungle-fishgame) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task jungle-fishgame)))
        (mark-text-as-seen *game-info* (text-id misty-level-name))
        )
      (((speedrun-category il-firecanyon))
        ;; skip keira intro cutscene
        (close-specific-task! (game-task firecanyon-assistant) (task-status need-reward-speech))
        ;; unlock blue eco vent
        (close-specific-task! (game-task jungle-eggtop) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task jungle-eggtop)))
        (mark-text-as-seen *game-info* (text-id firecanyon-level-name))
        )
      (((speedrun-category il-village2))
        ;; give enough orbs to buy all cells 120+120+90+90+90=510
        (set! (-> *game-info* money-total) 510.0)
        (set! (-> *game-info* money) 510.0)
        (mark-text-as-seen *game-info* (text-id village2-level-name))
        )
      (((speedrun-category il-sunken))
        (mark-text-as-seen *game-info* (text-id sunken-level-name))
        )
      (((speedrun-category il-swamp))
        ;; unlock flutflut
        (close-specific-task! (game-task beach-flutflut) (task-status need-resolution))
        (close-specific-task! (game-task village2-levitator) (task-status need-introduction))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task beach-flutflut)))
        (mark-text-as-seen *game-info* (text-id swamp-level-name))
        )
      (((speedrun-category il-rolling))
        ;; unlock zoomer
        (close-specific-task! (game-task village2-levitator) (task-status need-introduction))
        ;; unlock DMG
        (close-specific-task! (game-task rolling-race) (task-status need-introduction))
        ;; unlock moles
        (close-specific-task! (game-task rolling-moles) (task-status need-introduction))
        (mark-text-as-seen *game-info* (text-id rolling-level-name))
        )
      (((speedrun-category il-ogre))
        ;; skip klaww intro cutscene
        (close-specific-task! (game-task ogre-boss) (task-status need-hint))
        ;; unlock yellow eco vent
        (close-specific-task! (game-task snow-eggtop) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task snow-eggtop)))
        (mark-text-as-seen *game-info* (text-id ogre-level-name))
        )
      (((speedrun-category il-village3))
        ;; give enough orbs to buy all cells 120+120+90+90+90+90=600
        (set! (-> *game-info* money-total) 600.0)
        (set! (-> *game-info* money) 600.0)
        (mark-text-as-seen *game-info* (text-id village3-level-name))
        )
      (((speedrun-category il-snow))
        ;; unlock flutflut
        (close-specific-task! (game-task beach-flutflut) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task beach-flutflut)))
        ;; unlock gondola
        (close-specific-task! (game-task village3-button) (task-status need-introduction))
        (mark-text-as-seen *game-info* (text-id snow-level-name))
        )
      (((speedrun-category il-cave))
        ;; unlock yellow eco vent
        (close-specific-task! (game-task snow-eggtop) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task snow-eggtop)))
        (mark-text-as-seen *game-info* (text-id cave-level-name))
        )
      (((speedrun-category il-lavatube))
        ;; skip keira intro cutscene
        (close-specific-task! (game-task lavatube-start) (task-status need-reward-speech))
        (mark-text-as-seen *game-info* (text-id lavatube-level-name))
        )
      (((speedrun-category il-citadel))
        ;; remove invisible wall
        (close-specific-task! (game-task lavatube-end) (task-status need-resolution))
        (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (game-task lavatube-end)))
        ;; unlock door
        (close-specific-task! (game-task village4-button) (task-status need-reward-speech))
        (mark-text-as-seen *game-info* (text-id citadel-level-name))
        )
      (((speedrun-category new-game-plus))
        ;; skip intro cutscene
        (close-specific-task! (game-task intro) (task-status need-resolution))
        ;; unlock hub1 cells
        (dotimes (idx (length *hub1-cell-list*))
          (close-specific-task! (-> *hub1-cell-list* idx) (task-status need-resolution))
          (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (-> *hub1-cell-list* idx)))
          )
        ;; unlock hub2 cells
        (dotimes (idx (length *hub2-cell-list*))
          (close-specific-task! (-> *hub2-cell-list* idx) (task-status need-resolution))
          (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (-> *hub2-cell-list* idx)))
          )
        ;; unlock hub3 cells
        (dotimes (idx (length *hub3-cell-list*))
          (close-specific-task! (-> *hub3-cell-list* idx) (task-status need-resolution))
          (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (-> *hub3-cell-list* idx)))
          )
        )
      (((speedrun-category hub1-100))
        ;; skip intro cutscene
        (close-specific-task! (game-task intro) (task-status need-resolution))
        )
      (((speedrun-category hub2-100))
        ;; skip intro cutscene
        (close-specific-task! (game-task intro) (task-status need-resolution))
        ;; unlock hub1 cells
        (dotimes (idx (length *hub1-cell-list*))
          (close-specific-task! (-> *hub1-cell-list* idx) (task-status need-resolution))
          (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (-> *hub1-cell-list* idx)))
          )
        ;; give all orbs from hub1
        (set! (-> *game-info* money-total) 600.0)  ;; 50+50+150+150+150+50
        (set! (-> *game-info* money) 180.0)  ;; 50+50+150+150+150+50 - (90+90+120+120)
        )
      (((speedrun-category hub3-100))
        ;; skip intro cutscene
        (close-specific-task! (game-task intro) (task-status need-resolution))
        ;; unlock hub1 cells
        (dotimes (idx (length *hub1-cell-list*))
          (close-specific-task! (-> *hub1-cell-list* idx) (task-status need-resolution))
          (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (-> *hub1-cell-list* idx)))
          )
        ;; unlock hub2 cells
        (dotimes (idx (length *hub2-cell-list*))
          (close-specific-task! (-> *hub2-cell-list* idx) (task-status need-resolution))
          (send-event *target* 'get-pickup (pickup-type fuel-cell) (the float (-> *hub2-cell-list* idx)))
          )
        ;; give all orbs from hub1+2
        (set! (-> *game-info* money-total) 1300.0)  ;; 50+50+150+150+150+50 + 50+200+200+200+50
        (set! (-> *game-info* money) 370.0)  ;; 50+50+150+150+150+50 - (90+90+120+120) + 50+200+200+200+50 - (90+90+90+120+120)
        )
      (((speedrun-category all-cutscenes))
        ;; no post-blackout actions needed
        )
      (else
        (format 0 "setup-speedrun-post-blackout: unrecognized category ~S~%" (enum->string speedrun-category (-> *speedrun-info* category)))
        )
      )
      ;; reset speedrun display (gets hidden on first collectable pickup)
      (set! (-> *speedrun-info* should-display?) #t)
      ;; reset actors to ensure any above changes take effect
      (reset-actors 'life)
      ;; autosave any changes back to file
      (auto-save-command 'auto-save 0 0 *default-pool*)
    )
  (none)
  )

(defun hide-speedrun-display ()
  (set! (-> *speedrun-info* should-display?) #f)
  (none)
  )

(defun speedrun-start-full-game-run ()
  ;; start a full game speedrun, save file is already selected so don't default to first slot
  (start-speedrun (speedrun-category full-game))
  (none))

(defun draw-plane ((v1 vector) (v2 vector) (v3 vector) (v4 vector) (arg3 dma-buffer))
  (rlet ((vf27 :class vf))
    (vector-copy! (-> *boundary-polygon* 0) v1)
    (vector-copy! (-> *boundary-polygon* 3) v2)
    (vector-copy! (-> *boundary-polygon* 6) v3)
    (vector-copy! (-> *boundary-polygon* 9) v4)
    ;; colors?
    (let ((v1-1 (-> *boundary-polygon* 2)))
      (set! (-> v1-1 x) 128.0)
      (set! (-> v1-1 y) 128.0)
      (set! (-> v1-1 z) 128.0)
      (set! (-> v1-1 v w) 128.0)
      )
    (let ((v1-1 (-> *boundary-polygon* 5)))
      (set! (-> v1-1 x) 128.0)
      (set! (-> v1-1 y) 128.0)
      (set! (-> v1-1 z) 128.0)
      (set! (-> v1-1 v w) 128.0)
      )

    ;; idk?
    ;;  (cond
    ;;   (arg4
    (let ((v1-4 (-> *boundary-polygon* 1)))
      (set! (-> v1-4 x) 0.0)
      (set! (-> v1-4 y) 0.0)
      (set! (-> v1-4 z) 1.0)
      (set! (-> v1-4 v w) 1.0)
      )
    (let ((v1-6 (-> *boundary-polygon* 4)))
      (set! (-> v1-6 x) 1.0)
      (set! (-> v1-6 y) 0.0)
      (set! (-> v1-6 z) 1.0)
      (set! (-> v1-6 v w) 1.0)
      )
    (let ((v1-8 (-> *boundary-polygon* 7)))
      (set! (-> v1-8 x) 1.0)
      (set! (-> v1-8 y) 8.0)
      (set! (-> v1-8 z) 1.0)
      (set! (-> v1-8 v w) 1.0)
      )
    (let ((v1-10 (-> *boundary-polygon* 10)))
      (set! (-> v1-10 x) 0.0)
      (set! (-> v1-10 y) 8.0)
      (set! (-> v1-10 z) 1.0)
      (set! (-> v1-10 v w) 1.0)
      )
    ;;  )
    ;; (else
    ;;  (let ((v1-12 (-> *boundary-polygon* 1)))
    ;;   (set! (-> v1-12 x) 1.0)
    ;;   (set! (-> v1-12 y) 0.0)
    ;;   (set! (-> v1-12 z) 1.0)
    ;;   (set! (-> v1-12 v w) 1.0)
    ;;   )
    ;;  (let ((v1-14 (-> *boundary-polygon* 4)))
    ;;   (set! (-> v1-14 x) 0.0)
    ;;   (set! (-> v1-14 y) 0.0)
    ;;   (set! (-> v1-14 z) 1.0)
    ;;   (set! (-> v1-14 v w) 1.0)
    ;;   )
    ;;  (let ((v1-16 (-> *boundary-polygon* 7)))
    ;;   (set! (-> v1-16 x) 0.0)
    ;;   (set! (-> v1-16 y) 8.0)
    ;;   (set! (-> v1-16 z) 1.0)
    ;;   (set! (-> v1-16 v w) 1.0)
    ;;   )
    ;;  (let ((v1-18 (-> *boundary-polygon* 10)))
    ;;   (set! (-> v1-18 x) 1.0)
    ;;   (set! (-> v1-18 y) 8.0)
    ;;   (set! (-> v1-18 z) 1.0)
    ;;   (set! (-> v1-18 v w) 1.0)
    ;;   )
    ;;  )
    ;; )
    (init-boundary-regs)
    ;;(.lvf vf27 (&-> *sky-tng-data* giftag-roof qword))
    (set-sky-vf27 (&-> *sky-tng-data* giftag-roof qword))
    (render-boundary-quad (-> *boundary-polygon* 0) arg3)
    0
    (none)
    )
  )

(defun draw-deload-continue-info ()
  (when (and (-> *speedrun-info* should-display-deload-info?)
             (not (paused?)))
    (let ((y-offset (if (-> *speedrun-info* should-display?) 8 4))
          (backup-lvl
            (case (-> *backup-load-state* vis-nick)
              (('tra) training)
              (('vi1) village1)
              (('bea) beach)
              (('jun) jungle)
              (('jub) jungleb)
              (('mis) misty)
              (('fic) firecanyon)
              (('vi2) village2)
              (('sun) sunken)
              (('sub) sunkenb)
              (('swa) swamp)
              (('rol) rolling)
              (('ogr) ogre)
              (('vi3) village3)
              (('sno) snow)
              (('mai) maincave)
              (('dar) darkcave)
              (('rob) robocave)
              (('lav) lavatube)
              (('cit) citadel)
              (('fin) finalboss)
              (('dem) demo)
              (('tit) title)
              (else (the-as level-load-info #f))
              )
            )
          (continue_a (the-as continue-point #f))
          (a_dist 0.0)
          (continue_b (the-as continue-point #f))
          (b_dist 0.0)
          )
      (when (and ;; (= (-> s4-1 status) 'active)  ;; backed up level might not be loaded, but will be if deload happens
                 (null? (-> *level* load-commands)) ;; who cares
                 ;;  (= (-> s4-1 nickname) (-> *load-state* vis-nick))  ;; no need to check this, we're choosing level based on this already
                 (!= (-> backup-lvl name) (-> *game-info* current-continue level))
                 (-> *level* border?)
                 )
        (let* ((s2-0 (target-pos 0))
              (s4-2 (-> backup-lvl continues))
              (s1-0 (the-as continue-point (car s4-2)))
              )
          (while (not (null? s4-2))
            (when (zero? (-> s1-0 flags))
              (let ((t_dist (vector-vector-distance s2-0 (-> s1-0 trans))))
                (cond
                  ((or (not continue_a) (< t_dist a_dist))
                    ;; found new closest
                    (set! continue_b continue_a)  ;; old best is now second best
                    (set! b_dist a_dist)
                    (set! continue_a s1-0)
                    (set! a_dist t_dist)
                    )
                  ((or (not continue_b) (< t_dist b_dist))
                    ;; well we're at least closer than the old continue_b
                    (set! continue_b s1-0)
                    (set! b_dist t_dist)
                    )
                  )
                )
              )
              (set! s4-2 (cdr s4-2))
              (set! s1-0 (the-as continue-point (car s4-2)))
            )
          )
        )

      (with-dma-buffer-add-bucket ((buf (-> (current-frame) global-buf))
                                        (bucket-id subtitle))
        (draw-string-xy (string-format "Current continue: ~A ~%Backup level: ~A ~%Closest deload continue: ~A ~Mm~%2nd deload continue: ~A ~Mm"
                                        (-> *game-info* current-continue name)
                                        (-> backup-lvl name)
                                        (-> continue_a name)
                                        a_dist
                                        (-> continue_b name)
                                        b_dist
                                        )
                        buf 0 (- 220 (* 8 y-offset)) (font-color flat-yellow) (font-flags shadow kerning))
        )
      
      (when (and continue_a continue_b)
        (with-dma-buffer-add-bucket ((s5-0 (-> (current-frame) global-buf)) (bucket-id debug))
          (dma-buffer-add-gs-set-flusha s5-0
              (zbuf-1 (new 'static 'gs-zbuf :zbp #x1c0 :psm (gs-psm ct24)))
              (test-1 (new 'static 'gs-test :ate #x1 :atst (gs-atest greater-equal) :aref #x26 :zte #x1 :ztst (gs-ztest greater-equal)))
              (alpha-1 (new 'static 'gs-alpha :b #x1 :d #x1))
              )
          (with-cnt-vif-block-qwc (s5-0)
            (add-boundary-shader (new 'static 'texture-id :index #x33 :page #x2) s5-0)
              (let ((mid (new 'stack-no-clear 'vector))
                    (normal (new 'stack-no-clear 'vector))
                    (m_t (new 'stack-no-clear 'vector))
                    (sp_t 0.0)
                    (p_t (new 'stack-no-clear 'vector))
                    (normal1 (new 'stack-no-clear 'vector))
                    (normal2 (new 'stack-no-clear 'vector))
                    (v1 (new 'stack-no-clear 'vector))
                    (v2 (new 'stack-no-clear 'vector))
                    (v3 (new 'stack-no-clear 'vector))
                    (v4 (new 'stack-no-clear 'vector))
                    )
                ;; find midpoint
                (vector-average! mid (-> continue_a trans) (-> continue_b trans))
                ;; direction from A to B
                (vector-! normal (-> continue_b trans) (-> continue_a trans))
                ;; compute normal for plane
                (vector-normalize! normal 1.0)
                ;; find closest point on plane to target
                (vector-! m_t (target-pos 0) mid)
                (set! sp_t (vector-dot m_t normal))
                (vector--float*! p_t (target-pos 0) normal sp_t)
                ;; find rectangle coordinates
                (vector-cross! normal1 normal (new 'static 'vector :x 1.0))
                (vector-normalize! normal1 1.0)
                (vector-cross! normal2 normal (new 'static 'vector :y 1.0))
                (vector-normalize! normal2 1.0)
                (vector+float*! v1 p_t normal1 (meters 10.0))
                (vector+float*! v1 v1 normal2 (meters 10.0))
                (vector+float*! v2 p_t normal1 (meters -10.0))
                (vector+float*! v2 v2 normal2 (meters 10.0))
                (vector+float*! v3 p_t normal1 (meters -10.0))
                (vector+float*! v3 v3 normal2 (meters -10.0))
                (vector+float*! v4 p_t normal1 (meters 10.0))
                (vector+float*! v4 v4 normal2 (meters -10.0))
                ;; finally draw this mfer
                (draw-plane v1 v2 v3 v4 s5-0)
                )
              (close-sky-buffer s5-0)
            )
          )
        )
      )
    )
  (none)
  )

(defun speedrun-draw-settings ()
  "Draw speedrun related settings in the bottom left corner"
  (when (and (-> *pc-settings* speedrunner-mode?)
             (not (paused?))
             (-> *speedrun-info* should-display?))
    (with-dma-buffer-add-bucket ((buf (-> (current-frame) global-buf))
                                      (bucket-id subtitle))
      (draw-string-xy (string-format "Speedrunner Mode ~%OpenGOAL Practice Mod ~S ~%Category: ~S ~%Cutscene Skips ~A"
                                     *pc-settings-built-sha*
                                     (enum->string speedrun-category (-> *speedrun-info* category))
                                     (-> *pc-settings* skip-movies?))
                      buf 0 (- 220 (* 8 4)) (font-color flat-yellow) (font-flags shadow kerning))))
  (none))

(defun generic-post-init ()
  (when *last-real-continue-point*
    (set! (-> *game-info* current-continue) *last-real-continue-point*)
    )
  (none)
  )

(defun get-on-flutflut ()
  (generic-post-init)
  (case (-> *target* state)
    ((target-flut-stance target-racing)
      (send-event *target* 'end-mode)
      )
    (else
      (send-event *target* 'change-mode 'flut #f)
      )
    )
  (none)
  )

(defun get-on-zoomer ()
  (generic-post-init)
  (case (-> *target* state)
    ((target-flut-stance target-racing)
      (send-event *target* 'end-mode)
      )
    (else
      (send-event *target* 'change-mode 'racing #f)
      )
    )
  (none)
  )

(defun speedrun-reset-common-settings ()
  ;; disable hints (this seems to be overriden by your slot 1 save though)
  (set! (-> *setting-control* default play-hints) #f)
  ;; ensure `force actors` is not enabled
  (set! (-> *pc-settings* ps2-actor-vis?) #t)
  ;; force FPS to `60`
  (set-frame-rate! *pc-settings* 60 #t)
  ;; skip intro cutscene
  (close-specific-task! (game-task intro) (task-status need-resolution))
  (none)
  )

(defun full-hp ()
  (pickup-collectable! (-> *target* fact-info-target) (pickup-type eco-green) 1.0 (process->handle (-> *target* fact-info-target process)))
  (pickup-collectable! (-> *target* fact-info-target) (pickup-type eco-green) 1.0 (process->handle (-> *target* fact-info-target process)))
  (pickup-collectable! (-> *target* fact-info-target) (pickup-type eco-green) 1.0 (process->handle (-> *target* fact-info-target process)))
  (pickup-collectable! (-> *target* fact-info-target) (pickup-type eco-green) 1.0 (process->handle (-> *target* fact-info-target process)))
  (none)
  )

(define *tmp-continue-point* (new 'static 'continue-point
                             :name "tmp"
                             :level #f
                             :trans (new 'static 'vector :w 1.0)
                             :quat (new 'static 'quaternion :w 1.0)
                             :camera-trans (new 'static 'vector :w 1.0)
                             :camera-rot (new 'static 'array float 9)
                             :load-commands '()
                             :vis-nick #f
                             :lev0 #f
                             :disp0 #f
                             :lev1 #f
                             :disp1 #f
                             )
    )
(define *tmp-flut?* #f)
(define *tmp-zoomer?* #f)
(define *full-hp?* #f)

(defun custom-reset-multi-post-init ()
  (when *full-hp?* (full-hp))
  (cond
    (*tmp-flut?* (get-on-flutflut))
    (*tmp-zoomer?* (get-on-zoomer))
    )
  (none)
  )

(defun speedrun-practice-temp-checkpoint ()
  (when (-> *tmp-continue-point* level)
    (set! *practice-spawn-post-init* custom-reset-multi-post-init)
    ;; fully reset game if r2 held too
    (if (cpad-hold? 0 r2)  
      (initialize! *game-info* 'game (the-as game-save #f) "default")
      )
    ;; store last real checkpoint to restore later
    (when (not (string= (-> *game-info* current-continue name) "tmp"))
      (set! *last-real-continue-point* (-> *game-info* current-continue))
      )
    (set! (-> *game-info* current-continue) *tmp-continue-point*)
    (initialize! *game-info* 'dead (the-as game-save #f) (the-as string #f))
    (speedrun-reset-common-settings)
    )
  (none)
  )

(defun speedrun-practice-LTS-old-start-run ()
  (set! *practice-spawn-post-init* full-hp)
  (initialize! *game-info* 'game (the-as game-save #f) "village3-lts-old")
  (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "village3-lts-old"))
  (speedrun-reset-common-settings)
  (none)
  )

(defun speedrun-practice-LTS-new-start-run ()
  (set! *practice-spawn-post-init* full-hp)
  (initialize! *game-info* 'game (the-as game-save #f) "village3-lts-new")
  (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "village3-lts-new"))
  (speedrun-reset-common-settings)
  (none)
  )

(defun speedrun-practice-SFB-start-run ()
  (initialize! *game-info* 'game (the-as game-save #f) "lavatube-sfb")
  (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "lavatube-sfb"))
  (speedrun-reset-common-settings)
  (none)
  )

(defun speedrun-practice-FCS-start-run ()
  (initialize! *game-info* 'game (the-as game-save #f) "firecanyon-start")
  (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "firecanyon-start"))
  (speedrun-reset-common-settings)
  (none)
  )

(defun speedrun-practice-misty-boosted-start-run ()
  (initialize! *game-info* 'game (the-as game-save #f) "misty-boosted")
  (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "misty-boosted"))
  (speedrun-reset-common-settings)
  (none)
  )

(defun speedrun-practice-boulderskip-start-run ()
  (initialize! *game-info* 'game (the-as game-save #f) "village2-boulderskip")
  (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "village2-boulderskip"))
  (speedrun-reset-common-settings)
  (none)
  )

(defun speedrun-practice-piggyback-start-run ()
  (initialize! *game-info* 'game (the-as game-save #f) "sunken-piggyback")
  (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "sunken-piggyback"))
  (speedrun-reset-common-settings)
  (none)
  )

(defun speedrun-practice-buttonskip-start-run ()
  (initialize! *game-info* 'game (the-as game-save #f) "sunken-buttonskip")
  (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "sunken-buttonskip"))
  (speedrun-reset-common-settings)
  (none)
  )
  
(defun speedrun-practice-gondola-flut-start-run ()
  (set! *practice-spawn-post-init* get-on-flutflut)
  (initialize! *game-info* 'game (the-as game-save #f) "snow-gondola-flut")
  (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "snow-gondola-flut"))
  (speedrun-reset-common-settings)
  (none)
  )

(defun speedrun-practice-gondola-foot-start-run ()
  (initialize! *game-info* 'game (the-as game-save #f) "snow-gondola-foot")
  (set! (-> *game-info* current-continue) (get-continue-by-name *game-info* "snow-gondola-foot"))
  (speedrun-reset-common-settings)
  (none)
  )

(defun give-seven-flies ((lvl-idx int))
  ;; based on 'buzzer branch of (defmethod adjust game-info)
  (let* ((ctrl (get-task-control (-> *level-task-data* lvl-idx task-info (-> *level-task-data* lvl-idx buzzer-task-index) task-id)))
         (buzz-bits (get-reminder ctrl 0))
         (t9-10 (method-of-object ctrl save-reminder))
         )
    (logior! buzz-bits #b1111111) ;; all 7 bits enabled
    (t9-10 ctrl buzz-bits 0)
    )
  (set! (-> *game-counts* data lvl-idx buzzer-count) 7)
  (none)
  )

(defun close-task-helper ((tsk game-task))
  (logior! (-> *game-info* task-perm-list data (the int tsk) status) (entity-perm-status real-complete))
  (close-specific-task! tsk (task-status need-resolution))
  )

(defun continue-point-copy! ((dst continue-point) (src continue-point))
  (set! (-> dst level) (-> src level))
  (set! (-> dst quat y) 0.0)
  (set! (-> dst quat w) 1.0)
  (set! (-> dst load-commands) (-> src load-commands))
  (set! (-> dst vis-nick) (-> src vis-nick))
  (when (-> *level* level0)
    (set! (-> dst lev0) (-> *level* level0 name))
    (set! (-> dst lev0) (-> *level* level0 name))
    (set! (-> dst disp0) (-> *level* level0 display?))
    )
  (when (-> *level* level1)
    (set! (-> dst lev1) (-> *level* level1 name))
    (set! (-> dst disp1) (-> *level* level1 display?))
    )
  (none)
  )

(defun camera-rot-copy! ((dst continue-point) (src matrix))
  (set! (-> dst camera-rot 0) (-> src vector 0 x))
  (set! (-> dst camera-rot 1) (-> src vector 0 y))
  (set! (-> dst camera-rot 2) (-> src vector 0 z))
  (set! (-> dst camera-rot 3) (-> src vector 1 x))
  (set! (-> dst camera-rot 4) (-> src vector 1 y))
  (set! (-> dst camera-rot 5) (-> src vector 1 z))
  (set! (-> dst camera-rot 6) (-> src vector 2 x))
  (set! (-> dst camera-rot 7) (-> src vector 2 y))
  (set! (-> dst camera-rot 8) (-> src vector 2 z))
  (none)
  )

(defun speedrun-practice-store-temp-checkpoint ()
  ;; outside of bsp breaks shit
  (when (or (-> *level* level0 inside-boxes?) (-> *level* level1 inside-boxes?))
    (case (-> *target* state)
      ;; only store temp checkpoint if in one of these states
      ((target-stance target-wade-stance target-swim-stance target-flut-stance target-racing)
        (set! *last-speedrun-practice-option* (speedrun-practice-option none))
        
        ;; assume current continue is good enough for the current location
        (continue-point-copy! *tmp-continue-point* (-> *game-info* current-continue))
        (vector-copy! (-> *tmp-continue-point* trans) (-> *target* root trans))
        (quaternion-copy! (-> *tmp-continue-point* quat) (-> *target* control unknown-quaternion00))
        (vector-copy! (-> *tmp-continue-point* camera-trans) (-> *camera-combiner* trans))
        (camera-rot-copy! *tmp-continue-point* (-> *camera-combiner* inv-camera-rot))

        (set! *full-hp?* (and (!= *target* #f)
                      (= (-> *target* fact-info-target health) 3.0)
                      (= (-> *target* fact-info-target eco-pill) 50.0)
                      ))
        (set! *tmp-flut?* #f)
        (set! *tmp-zoomer?* #f)
        (case (-> *target* state)
          ((target-flut-stance)
            (set! *tmp-flut?* #t)
            )
          ((target-racing)
            (set! *tmp-zoomer?* #t)
            )
          )
        (sound-play "select-menu")
        )
      )
    )
  (none)
  )

(defun speedrun-practice-spawn ()
  ;; enable speedrunner mode
  (set! (-> *pc-settings* speedrunner-mode?) #t)
  ;; randomize game id so the autosplitter knows to restart
  (update-autosplit-jak1-new-game)

  ;; reset any previous post-target-init callback
  (set! *practice-spawn-post-init* #f)

  (case *last-speedrun-practice-option*
    (((speedrun-practice-option lts-old))
      (speedrun-practice-LTS-old-start-run)
      )
    (((speedrun-practice-option lts-new))
      (speedrun-practice-LTS-new-start-run)
      )
    (((speedrun-practice-option sfb))
      (speedrun-practice-SFB-start-run)
      )
    (((speedrun-practice-option fcs))
      (speedrun-practice-FCS-start-run)
      )
    (((speedrun-practice-option misty-boosted))
      (speedrun-practice-misty-boosted-start-run)
      )
    (((speedrun-practice-option boulderskip))
      (speedrun-practice-boulderskip-start-run)
      )
    (((speedrun-practice-option piggyback))
      (speedrun-practice-piggyback-start-run)
      )
    (((speedrun-practice-option buttonskip))
      (speedrun-practice-buttonskip-start-run)
      )
    (((speedrun-practice-option gondola-flut))
      (speedrun-practice-gondola-flut-start-run)
      )
    (((speedrun-practice-option gondola-foot))
      (speedrun-practice-gondola-foot-start-run)
      )
    (else
      (speedrun-practice-temp-checkpoint)
      )
    )
  
  (none)
  )
  
(defun speedrun-mode-update ()
  "A per frame update for speedrunning related stuff"
  (when (-> *pc-settings* speedrunner-mode?)
    ;; Update auto-splitter
    (update-autosplit-info-jak1)
    ;; Draw info to the screen
    (draw-deload-continue-info)
    (speedrun-draw-settings)
    ;;Disable any active cheats
    (set! (-> *pc-settings* cheats) (the-as pc-cheats #x0))
    ;; Run after-blackout speedrun setup (if needed)
    (setup-speedrun-post-blackout)

    ;; call post-init function
    (when (and *practice-spawn-post-init*
               *target*
               (-> *setting-control* current allow-pause)
               (>= (- (-> *display* base-frame-counter) (-> *game-info* blackout-time)) (seconds 0.1))
               )
      (*practice-spawn-post-init*)
      (set! *practice-spawn-post-init* #f)
      )

    (when (cpad-hold? 0 l2)
      ;; Check for fast reset combo
      (when (cpad-pressed? 0 up)
        (format #t "player triggered reset combo~%")
        (speedrun-practice-spawn)
        )
      
      ;; Check for temp checkpoint combo
      (when (cpad-pressed? 0 down)
        (format #t "player triggered temp checkpoint combo~%")
        (speedrun-practice-store-temp-checkpoint)
        )
      
      ;; Check for flutflut
      (when (cpad-pressed? 0 left)
        (format #t "player triggered flutflut combo~%")
        (get-on-flutflut)
        )
      
      ;; Check for zoomer
      (when (cpad-pressed? 0 right)
        (format #t "player triggered zoomer combo~%")
        (get-on-zoomer)
        )
      )
    )
  (none))